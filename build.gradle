
/* The authors of Geriatric Ice Vakib release this file under Mozilla Public v2.0 license terms. */

import com.github.jk1.license.render.*

plugins {
	id 'com.github.jk1.dependency-license-report' version '2.0' // ¶ task generateLicenseReport to see dependency licenses
	id 'com.github.ben-manes.versions' version '0.42.0'  // ¶ task dependencyUpdates to see updated libraries
	id 'java'
	// id 'com.github.johnrengelman.shadow' version '7.1.2' // ¶ for uber jar
	id 'signing'
	id 'maven-publish'
	// id 'distribution' // ¶ for zip file with readme
	id 'application' // ¶ for run task
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
	annotationProcessor 'info.picocli:picocli-codegen:4.6.1'
	implementation 'info.picocli:picocli:4.6.1'
	implementation 'org.slf4j:slf4j-api:1.7.25'
	implementation 'org.slf4j:slf4j-simple:1.7.25'
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.2'
}

group = 'com.beyondrelations.fs.giv'
version = '2.1'
description = 'Remove deprecated jars'

compileJava {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
}

sourceCompatibility = '1.8'

// ¶ https://github.com/jk1/Gradle-License-Report#configuration
licenseReport {
	renderers = [ new JsonReportRenderer('dependency-libraries.json', false ) ]
}

// ¶ https://docs.gradle.org/current/userguide/building_java_projects.html
java {
	withSourcesJar()
	withJavadocJar()
}

// ¶ https://docs.gradle.org/current/userguide/java_testing.html
test {
	useJUnitPlatform()
	testLogging {
		exceptionFormat = 'full'
	}
}

jar {
	manifest {
		attributes(
			'Main-Class' : 'com.beyondrelations.fs.giv.InitGiv',
			'Automatic-Module-Name' : 'com.beyondrelations.fs.giv',
		)
	}
	from "LICENSE" // ¶ embed this in the jar itself
}

// ¶ to change the default shadowJar filename // https://imperceptiblethoughts.com/shadow/configuration/

// ¶ https://docs.gradle.org/current/userguide/application_plugin.html
run {
	mainClassName = 'com.beyondrelations.fs.giv.InitGiv'
}

task run_( type: JavaExec ) {
	group = 'application'
	classpath = sourceSets.main.runtimeClasspath
	mainClass = 'com.beyondrelations.fs.giv.InitGiv'
	standardInput = System.in
}

// ¶ signs jar file with keystore certificate
task jsin( type:Exec, dependsOn: jar ) {
	workingDir project.projectDir
	commandLine project.js_jarsigner,
			// '-verbose',
			'-keystore', project.js_keyStore,
			'-storepass', project.js_keyStorePass,
			// '-keypass', project.js_keyStoreKeyPass,
			'-tsa', project.js_tsa,
			'build/libs/'+ project.name +'-'+ project.version +'.jar',
			project.js_keyStoreAlias
}

task jvify( type:Exec, dependsOn: jar ) {
	workingDir project.projectDir
	commandLine project.js_jarsigner,
			// '-verbose',
			'-verify',
			'build/libs/'+ project.name +'-'+ project.version +'.jar'
}

// ¶ https://docs.gradle.org/current/userguide/signing_plugin.html
signing {
	sign publishing.publications
	useGpgCmd()
}

// ¶ https://docs.gradle.org/current/userguide/publishing_maven.htm
publishing {
	publications {
		mavenJava( MavenPublication ) {
			from( components.java )
			pom {
				name = project.name
				description = project.description
				inceptionYear = '2021'
				url = 'https://github.com/BR-Nicholas-Prado/Geriatric_Ice_Vakib'
				packaging = 'jar'
				licenses {
					license {
						name = 'Mozilla Public v2.0'
						url = 'https://www.mozilla.org/en-US/MPL/2.0'
					}
				}
				developers {
					developer {
						id = 'BR-Nicholas-Prado'
						name = 'Nicholas Prado'
						email = 'nick@getworkflo.com'
						timezone = 'America/Chicago'
						url = 'www.getworkflo.com'
					}
				}
				scm {
					connection = 'https://github.com/BR-Nicholas-Prado/Geriatric_Ice_Vakib.git'
					developerConnection = 'git@github.com:BR-Nicholas-Prado/Geriatric_Ice_Vakib.git'
					url = 'https://github.com/BR-Nicholas-Prado/Geriatric_Ice_Vakib'
				}
			}
			// artifact distZip
		}
	}
	repositories {
		mavenLocal()
		// mavenCentral()
	}
}

/*
// ¶ https://docs.gradle.org/current/userguide/distribution_plugin.html
distributions {
	main {
		distributionBaseName = 'givakib_with_readme'
		contents {
			from 'LICENSE',
				'readme.adoc',
				'build.gradle',
				'build/report/dependency-libraries.json',
				'changelog.adoc'
		}
	}
}
*/

// ¶ ensures that this signs jar contents before gpg signing
tasks.withType( PublishToMavenRepository.class ) {
	dependsOn( 'jsin' )
}












